<HTML>
<HEAD>
<META HTTP-EQUIV="CONTENT-TYPE" CONTENT="TEXT/HTML; CHARSET=UTF-8">
<TITLE>Distance-Regular Graphs of Classical Parameters</TITLE>
    <script src="https://sagecell.sagemath.org/static/embedded_sagecell.js"></script>
    <script>
    sagecell.makeSagecell({inputLocation: 'div.compute',
                           evalButtonText: 'Evaluate'});
    </script>
</HEAD>
<BODY BGCOLOR = "#CCFFCC">
<A NAME = "top"></A>
<div align = "right"><I>Last Update: July 18, 2020</I></div>
<HR>

<H2>Distance-Regular Graphs of Classical Parameters (d, q, a, b)</H2>

<ul>
<li> Click the “Evaluate” button below to calculate parameters.
<li> The outputs will be at the bottom.
<li> Edit the parameters in "array" at the top of the code, and see the results.
<li> The program below only works for numeric values.
<li> If you want to get the text form of matrices, edit 'show' to 'print'
<li> You can get latex-format output as shown below.
<p>
<li> <a href="https://sagecell.sagemath.org">Link to SageCell Server</a>
<li> <a href="https://sagecell.sagemath.org/static/about.html?v=a01a6feae5b07628bcaac5676b74bf5e">Instruction to make similar pages</a>
</ul>

<H3>Examples of Distance-Regular Graphs of Classical Parameters</H3>

<ol>
<li>Johnson graph J(n,d): (d, 1, 1, n-d)
<li>Grassmann graph J<sub>q</sub>(n,d): (d, q, q, q[n-d]<sub>q</sub>)
<li>Dual polar graph (e = 0, 1/2, 1, 3/2, 2): (d,q,0,q<sup>e</sup>)
<li>Dual polar graph U(2d,r): (d, -r, r(r+1)/(1-r), (r-(-r)<sup>d+1</sup>)/(1-r))
<li>Half dual polar graph: D<sub>n.n</sub>(q): (d, q<sup>2</sup>, q<sup>2</sup>+q, q[m]<sub>q</sub>),  [m=n=2d+1, or m+1 = n = 2d]
<li>Exceptional Lie graph E<sub>7,7</sub>(q): (3, q<sup>4</sup>, (q<sup>3</sup>+q<sup>2</sup>+q+1)q, q[9]<sub>q</sub>)
<li>Gosset graph E<sub>7</sub>(1): (3, 1, 4, 9)
<li>Triality graph <sup>3</sup>D<sub>4,2</sub>(q): (3, -q, q/(1-q), q(q+1))
<li>Witt graph M<sub>24</sub>: (3, -2, -4, 10)
<li>Witt graph M<sub>23</sub>: (3, -2, -2, 5)
<li>Hamming graph H(d,n): (d, 1, 0, n-1)
<li>Halved cube (1/2)H(n,2): (d,1,2,m), [m=n=2d+1, or m+1 = n = 2d]
<li>Bilinear forms graph: (d, q, q-1, q<sup>n</sup>-1)
<li>Alternating forms graph: (d, q<sup>2</sup>, q<sup>2</sup>-1, q<sup>m</sup>-1), [m=n=2d+1, or m+1 = n = 2d]
<li>Heamitean forms graph q = r<sup>2</sup>: (d, -r, -r-1, -(-r)<sup>d</sup>-1)
<li>Affine E<sub>6</sub>(q) graph: (3, q<sup>4</sup>, q<sup>4</sup>-1, q<sup>9</sup>-1)
<li>Extended ternary Golay code graph: (3, -2, -3, 8)
<li>Pseudo D<sub>m</sub>(q) graphs: (d, q, 0, 1)
<li>Dist. 1-or-2 in symplectic dual polar graph: (d, q<sup>2</sup>, q(q+1), q[m]<sub>q</sub>)
<li>Doob graph: (d, 1, 0, 3)
<li>Quadratic forms graph: (d, q<sup>2</sup>, q<sup>2</sup>-1, q<sup>m</sup>-1), , [m=n=2d-1, or m-1 = n = 2d]
</ol>

<div class="compute"><script type="text/x-sage">

# Default parameters for (d, q, alpha, beta)
array = [4,-3,-2,-41]

# Program of Functions
# q-binary coefficient
def bq(q,n):
    if q != 1:
        return (q^n-1)/(q-1)
    else:
        return n

# [b0, b1, b2, ... , bd]
def bi(array):
    d = array[0]; q = array[1]; a = array[2]; b = array[3]
    bi = []
    for i in range(0,d+1):
        bi.append((bq(q,d)-bq(q,i))*(b-a*bq(q,i)))
    return bi

# [c0, c1, c2, ... , cd]
def ci(array):
    d = array[0]; q = array[1]; a = array[2]; b = array[3]
    ci = [0]
    for i in range(1,d+1):
        ci.append(bq(q,i)*(1+a*bq(q,i-1)))
    return ci

# [a0, a1, a2, ... , ad]
def ai(array):
    d = array[0]; q = array[1]; a = array[2]; b = array[3]
    ai = [0]
    for i in range(1,d+1):
        ai.append(bq(q,i)*(b-1+a*(bq(q,d)-bq(q,i)-bq(q,i-1))))
    return ai

# intersection array
def intersection_array(array):
    return matrix([ci(array), ai(array), bi(array)])

# intersection matrix B = B1
def intersection_matrix(array):
    d = array[0]
    B1 = zero_matrix(d+1)
    for i in range(0,d):
        B1[i+1,i] = bi(array)[i]
    for i in range(0,d+1):
        B1[i,i] = ai(array)[i]
    for i in range(0,d):
        B1[i,i+1] = ci(array)[i+1]
    return B1

# intersection matrices [B1, B2, ... , Bd]
def intersection_matrices(array):
    d = array[0]
    B = [identity_matrix(d+1), intersection_matrix(array)]
    X = B[1]
    for i in range(2,d+1):
        B.append((1/ci(array)[i])*(X*B[i-1] - ai(array)[i-1]*B[i-1] - bi(array)[i-2]*B[i-2]))
    return B

# degrees [k0, k1, ..., kd]
def ki(array):
    d = array[0]
    ki = [1]
    for i in range(1,d+1):
        ki.append(ki[i-1]*bi(array)[i-1]/ci(array)[i])
    return ki

# P matrix: the first eigen matrix
def first_eigenmatrix(array):
    d = array[0]
    P = zero_matrix(d+1)
    ev = intersection_matrix(array).eigenvalues()
    for i in range(0,d+1):
        P[i,0] = 1
        P[i,1] = ev[i]
        for j in range(2,d+1):
            P[i,j] = (1/ci(array)[j])*(ev[i]*P[i,j-1] - ai(array)[j-1]*P[i,j-1] - bi(array)[j-2]*P[i,j-2])
    return P

# multiplicities [m0, m1, ... , md]
def mi(array):
    d = array[0]
    mi = [1]
    n = sum([ki(array)[i] for i in range(0,d+1)])
    P = first_eigenmatrix(array)
    for i in range(1,d+1):
        mi.append(n/(sum([P[i,j]^2/ki(array)[j] for j in range(0,d+1)])))
    return mi

# Q matrix: the second eigen matrix
def second_eigenmatrix(array):
    d = array[0]
    Q = zero_matrix(QQ, d+1)
    P = first_eigenmatrix(array)
    m = mi(array)
    k = ki(array)
    for i in range(0,d+1):
        for j in range(0,d+1):
            Q[i,j] = m[j]*P[j,i]/k[i]
    return Q

# Outputs for the array defined above
show("Intersection Array", "=", intersection_array(array))

show("Degrees", "=", ki(array))

show("Multiplicities", "=", mi(array))

show("First Eigenmatrix", "=", first_eigenmatrix(array))

show("Second Eigenmatrix", "=", second_eigenmatrix(array))

show("Intersection Matrices", "=", intersection_matrices(array))

print("Latex Outputs")

latex(first_eigenmatrix(array))

# latex(second_eigenmatrix(array))
</script></div>

<hr>
<DIV ALIGN = RIGHT><TABLE BORDER = 3 BGCOLOR = "#FFFFFF"><TR>
<TD><A HREF = "#top">TOP</TD>
<TD><A HREF = "sage.html">Sage</A></TD>
<TD><A HREF = "index.html">CW in ME</A></TD>
<TD><A HREF = "../../index-j.html">HOME (J)</A></TD>
<TD><A HREF = "../../index.html">HOME (E)</A></TD>
</TR>
</TABLE>
</DIV>
</BODY>
</HTML>
